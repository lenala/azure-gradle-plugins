/*
 * MIT License
 * 
 * Copyright (c) Microsoft Corporation. All rights reserved.
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'com.gradle.plugin-publish' version '0.10.0' apply false
    id 'com.gradle.build-scan'     version '2.1'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'java-gradle-plugin'
    apply plugin: 'com.gradle.plugin-publish'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    compileGroovy {
        sourceCompatibility = project.sourceCompatibility
        targetCompatibility = project.targetCompatibility
    }

    dependencies {
        compile gradleApi()
        compile "com.microsoft.azure:azure:$azureVersion"
        compile "commons-net:commons-net:$commonsNetVersion"
        compile "commons-io:commons-io:$commonsIoVersion"
        compile "org.zeroturnaround:zt-zip:$ztZipVersion"
        compile "org.reflections:reflections:$reflectionsVersion"

        testCompile gradleTestKit()
        testCompile "junit:junit:$junitVersion"
    }

    gradlePlugin {
        plugins {
            "${project.pluginShortName}Plugin" {
                id                  = project.pluginId
                implementationClass = project.pluginImplementationClass
            }
        }
    }

    pluginBundle {
        website     = 'https://github.com/lenala/azure-gradle-plugins'
        vcsUrl      = 'https://github.com/lenala/azure-gradle-plugins.git'
        description = project.projectDescription
        tags        = ['azure']

        plugins {
            "${project.pluginShortName}Plugin" {
                id          = project.pluginId
                displayName = project.displayName
            }
        }

        mavenCoordinates {
            groupId    = project.group
            artifactId = project.name
            version    = project.version
        }
    }
}